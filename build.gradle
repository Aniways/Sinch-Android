buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

task nativeLibsToJar(type: Zip, description: 'create a jar archive of the native libs') {
    destinationDir file("/$buildDir/native-libs")
    baseName 'native-libs'
    extension 'jar'
    from fileTree(dir: 'libs', include: '**/*.so')
    into 'lib/'
}

tasks.withType(Compile) { compileTask -> compileTask.dependsOn nativeLibsToJar }

tasks.withType(com.android.build.gradle.tasks.PackageApplication) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
}

dependencies {
    // workaround for native-libs not being included in first build after clean
    def nativeLibs = new File("$buildDir/native-libs")
    if (!nativeLibs.exists()) {
        nativeLibs.mkdirs();
    }
    new File("$nativeLibs/native-libs.jar").createNewFile()
    compile fileTree(dir: nativeLibs)
    // end ugly workaround

    compile files("$projectDir/libs/sinch-android-rtc-3.1.0.jar")
    compile fileTree(dir: "/$buildDir/native-libs", include: 'native-libs.jar')
}

